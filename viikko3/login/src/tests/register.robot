*** Settings ***
Resource  resource.robot
Suite Setup     Open And Configure Browser
Suite Teardown  Close Browser
Test Setup      Reset Application Create User And Go To Register Page

*** Test Cases ***

Register With Valid Username And Password
    Set Username   matti
    Set Password   matti1234!
    Set Password Confirmation   matti1234!
    Click Button   Register
    Registration Should Succeed

Register With Too Short Username And Valid Password
    Set Username   ab
    Set Password   validpass1!
    Set Password Confirmation   validpass1!
    Click Button   Register
    Registration Should Fail With Message   Username must be at least 3 characters long

Register With Valid Username And Too Short Password
    Set Username   pekka
    Set Password   short
    Set Password Confirmation   short
    Click Button   Register
    Registration Should Fail With Message   Password must be at least 8 characters long

Register With Valid Username And Invalid Password
    Set Username   olli
    Set Password   fffffffff
    Set Password Confirmation   fffffffff
    Click Button   Register
    Registration Should Fail With Message   Password cannot consist of only letters

Register With Nonmatching Password And Password Confirmation
    Set Username   jaana
    Set Password   validpass1!
    Set Password Confirmation   validpass1?
    Click Button   Register
    Registration Should Fail With Message   Passwords do not match

Register With Username That Is Already In Use
    Create User   kalle   kalle123!
    Go To Register Page
    Set Username   kalle
    Set Password   kalle123!
    Set Password Confirmation   kalle123!
    Click Button   Register
    Registration Should Fail With Message   Username already exists

*** Keywords ***

Reset Application Create User And Go To Register Page
    Reset Application
    Create User  kalle  kalle123
    Go To Register Page

Registration Should Succeed
    Main Page Should Be Open

Registration Should Fail With Message
    [Arguments]   ${message}
    Register Page Should Be Open
    Page Should Contain   ${message}

Set Username
    [Arguments]   ${username}
    Input Text    username    ${username}

Set Password
    [Arguments]   ${password}
    Input Password    password   ${password}

Set Password Confirmation
    [Arguments]   ${password}
    Input Password   password_confirmation   ${password}
